#! /bin/csh

#
# @(#)checkmanpage	1.1 96/04/11 SMI
#
# check a manpage for correctness
#

if ("$?PRINTER" == 0) then
  echo -n 'Enter name of printer to print to: '
  setenv PRINTER "$<"
endif

if ($#argv == "0") then
  echo "`basename $0`: must specify one or more names of manual pages to print"
  exit 1
endif

@ errors = 0

foreach FILE ($*)
if (! -e "$FILE") then
  echo "${FILE}: does not exist - cannot be printed"
  @ errors = $errors + 1
  continue
endif
if (-d "$FILE") then
  echo "${FILE}: is a directory - cannot be printed"
  @ errors = $errors + 1
  continue
endif
if (! -f "$FILE") then
  echo "${FILE}: not a regular file - cannot be printed"
  @ errors = $errors + 1
  continue
endif
file ${FILE} | fgrep -s -i 'roff'
if ("${status}" != 0) then
  echo "${FILE}: not a manual page - cannot be printed"
  @ errors = $errors + 1
  continue
endif
end

if ($errors != 0) then
  echo "errors in naming files to print: no files printed"
  exit 1
endif

if (! -e /usr/doctools/bin/doclint.man) then
  echo "Cant find document check processor"
  exit 1
endif

foreach FILE ($*)
  echo "Checking manual page: ${FILE}"
  /usr/doctools/bin/doclint.man ${FILE} |& more -s
end

exit 1

